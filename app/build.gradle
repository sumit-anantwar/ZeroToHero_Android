apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Accessor for local.properties
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion build_config.compileSdkVersion

    defaultConfig {
        applicationId build_config.package
        minSdkVersion build_config.minSdkVersion
        targetSdkVersion build_config.targetSdkVersion
        versionCode version_code
        versionName version_name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Read the Crashlytics Key from local.properties
        manifestPlaceholders = [crashlytics_key: properties.get("crashlytics.key")]
    }

    buildTypes {

        debug {
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "CC Player DEV"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "CC Player"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:${dependency.kotlin}"

    // Android Support
    implementation "com.android.support:appcompat-v7:${dependency.support}"
    implementation "com.android.support.constraint:constraint-layout:${dependency.constraint_layout}"

    // Timber
    implementation "com.jakewharton.timber:timber:${dependency.timber}"

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true;
    }


    // Tests
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}


buildscript {

    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

apply plugin: 'io.fabric'